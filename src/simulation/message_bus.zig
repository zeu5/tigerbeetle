const std = @import("std");
const assert = std.debug.assert;

const MessagePool = @import("../message_pool.zig").MessagePool;
const Message = MessagePool.Message;
const vsr = @import("../vsr.zig");
const ProcessType = vsr.ProcessType;

const NetworkType = @import("network.zig").NetworkType;

pub const Process = union(ProcessType) {
    replica: u8,
    client: u128,
};

pub fn MessageBusType(comptime NetworkSimulatorType: anytype) type {
    const Network = NetworkType(NetworkSimulatorType);

    return struct {
        const MessageBus = @This();
        network: *Network,
        pool: *MessagePool,

        cluster: u128,
        process: Process,

        /// The callback to be called when a message is received.
        on_message_callback: *const fn (message_bus: *MessageBus, message: *Message) void,

        pub const Options = struct {
            network: *Network,
        };

        pub fn init(
            _: std.mem.Allocator,
            cluster: u128,
            process: Process,
            message_pool: *MessagePool,
            on_message_callback: *const fn (message_bus: *MessageBus, message: *Message) void,
            options: Options,
        ) !MessageBus {
            return MessageBus{
                .network = options.network,
                .pool = message_pool,
                .cluster = cluster,
                .process = process,
                .on_message_callback = on_message_callback,
            };
        }

        /// TODO
        pub fn deinit(_: *MessageBus, _: std.mem.Allocator) void {}

        pub fn tick(_: *MessageBus) void {}

        pub fn get_message(
            bus: *MessageBus,
            comptime command: ?vsr.Command,
        ) MessagePool.GetMessageType(command) {
            return bus.pool.get_message(command);
        }

        /// `@TypeOf(message)` is one of:
        /// - `*Message`
        /// - `MessageType(command)` for any `command`.
        pub fn unref(bus: *MessageBus, message: anytype) void {
            bus.pool.unref(message);
        }

        pub fn send_message_to_replica(bus: *MessageBus, replica: u8, message: *Message) void {
            // Messages sent by a process to itself should never be passed to the message bus
            if (bus.process == .replica) assert(replica != bus.process.replica);

            bus.network.send_message(message, .{
                .source = bus.process,
                .target = .{ .replica = replica },
            });
        }

        /// Try to send the message to the client with the given id.
        /// If the client is not currently connected, the message is silently dropped.
        pub fn send_message_to_client(bus: *MessageBus, client_id: u128, message: *Message) void {
            assert(bus.process == .replica);

            bus.network.send_message(message, .{
                .source = bus.process,
                .target = .{ .client = client_id },
            });
        }
    };
}
